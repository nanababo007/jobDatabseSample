
/*
 * 게시글 저장(등록/수정) PL/SQL 구문
 */
DECLARE
	V_SAVE_METHOD VARCHAR2(1); -- 'B'(기본 INSERT/UPDATE 구문), 'M'(MERGE INTO 구문)
	V_BOARD_ARTICLE_NO TB_BOARD_ARTICLE.BOARD_ARTICLE_NO%TYPE; -- 0(신규등록), 1~(정보변경)
	V_BOARD_ARTICLE_TITLE TB_BOARD_ARTICLE.BOARD_ARTICLE_TITLE%TYPE;
	V_BOARD_NO TB_BOARD_ARTICLE.BOARD_NO%TYPE;
	V_BOARD_ARTICLE_CONTENT TB_BOARD_ARTICLE.BOARD_ARTICLE_CONTENT%TYPE;
	V_BOARD_ARTICLE_REPLY_CONTENT TB_BOARD_ARTICLE.BOARD_ARTICLE_REPLY_CONTENT%TYPE;
	V_NOTICE_YN TB_BOARD_ARTICLE.NOTICE_YN%TYPE;
	V_REGUSER TB_BOARD_ARTICLE.REGUSER%TYPE;
	V_MODUSER TB_BOARD_ARTICLE.MODUSER%TYPE;
	---
	V_BOARD_CHECK NUMBER(10,0);
	---
	VALID_CHECK_ERROR EXCEPTION;
	NO_BOARD_ERROR EXCEPTION;
	NO_SAVE_METHOD_ERROR EXCEPTION;
BEGIN
	--- 게시판 등록 정보 설정
	V_SAVE_METHOD := 'B';
	V_BOARD_ARTICLE_NO := 0;
	V_BOARD_NO := 1;
	V_BOARD_ARTICLE_TITLE := '게시글01';
	V_BOARD_ARTICLE_CONTENT := '게시글 내용 01';
	V_BOARD_ARTICLE_REPLY_CONTENT := '게시글 답변 내용 01';
	V_NOTICE_YN := 'N';
	V_REGUSER := 'ADMIN';
	V_MODUSER := NULL;
	---
	V_BOARD_CHECK := 0;
	--- 유효성 확인
	IF V_BOARD_ARTICLE_TITLE IS NULL THEN
		RAISE VALID_CHECK_ERROR;
	END IF;
	---
	SELECT COUNT(*) INTO V_BOARD_CHECK
	FROM TB_BOARD A
	WHERE A.BOARD_NO = V_BOARD_NO;
	IF V_BOARD_CHECK = 0 THEN
		RAISE NO_BOARD_ERROR;
	END IF;
	--- 게시판 입력 처리
	IF V_SAVE_METHOD = 'B' THEN
		IF V_BOARD_ARTICLE_NO = 0 THEN
			INSERT INTO TB_BOARD_ARTICLE (
				BOARD_ARTICLE_NO,
				BOARD_ARTICLE_TITLE,
				BOARD_NO,
				BOARD_ARTICLE_VIEW_CNT,
				BOARD_ARTICLE_CONTENT,
				BOARD_ARTICLE_REPLY_CONTENT,
				REGDATE,
				REGUSER,
				MODDATE,
				MODUSER
			)VALUES(
				TB_BOARD_ARTICLE_SEQ.NEXTVAL,
				V_BOARD_ARTICLE_TITLE,
				V_BOARD_NO,
				0,
				V_BOARD_ARTICLE_CONTENT,
				V_BOARD_ARTICLE_REPLY_CONTENT,
				SYSDATE,
				V_REGUSER,
				NULL,
				NULL
			);
		ELSE
			UPDATE TB_BOARD_ARTICLE SET
				BOARD_ARTICLE_TITLE = V_BOARD_ARTICLE_TITLE,
				BOARD_NO = V_BOARD_NO,
				BOARD_ARTICLE_CONTENT = V_BOARD_ARTICLE_CONTENT,
				BOARD_ARTICLE_REPLY_CONTENT = V_BOARD_ARTICLE_REPLY_CONTENT,
				NOTICE_YN = V_NOTICE_YN,
				MODDATE = SYSDATE,
				MODUSER = V_MODUSER
			WHERE BOARD_ARTICLE_NO = V_BOARD_ARTICLE_NO;
		END IF;
	ELSIF V_SAVE_METHOD = 'M' THEN
		MERGE INTO TB_BOARD_ARTICLE A
		USING DUAL
			ON (A.BOARD_ARTICLE_NO = V_BOARD_ARTICLE_NO)
		WHEN MATCHED THEN
			UPDATE SET
				BOARD_ARTICLE_TITLE = V_BOARD_ARTICLE_TITLE,
				BOARD_NO = V_BOARD_NO,
				BOARD_ARTICLE_CONTENT = V_BOARD_ARTICLE_CONTENT,
				BOARD_ARTICLE_REPLY_CONTENT = V_BOARD_ARTICLE_REPLY_CONTENT,
				NOTICE_YN = V_NOTICE_YN,
				MODDATE = SYSDATE,
				MODUSER = V_MODUSER
		WHEN NOT MATCHED THEN
			INSERT (
				BOARD_ARTICLE_NO,
				BOARD_ARTICLE_TITLE,
				BOARD_NO,
				BOARD_ARTICLE_VIEW_CNT,
				BOARD_ARTICLE_CONTENT,
				BOARD_ARTICLE_REPLY_CONTENT,
				REGDATE,
				REGUSER,
				MODDATE,
				MODUSER
			)VALUES(
				TB_BOARD_ARTICLE_SEQ.NEXTVAL,
				V_BOARD_ARTICLE_TITLE,
				V_BOARD_NO,
				0,
				V_BOARD_ARTICLE_CONTENT,
				V_BOARD_ARTICLE_REPLY_CONTENT,
				SYSDATE,
				V_REGUSER,
				NULL,
				NULL
			);
	ELSE
		RAISE NO_SAVE_METHOD_ERROR;
	END IF;
	---
	COMMIT;
	--- 결과 메시지 출력
	DBMS_OUTPUT.PUT_LINE('성공적으로 처리 되었습니다.');
	--- 예외 처리
	EXCEPTION
	WHEN VALID_CHECK_ERROR THEN
		DBMS_OUTPUT.PUT_LINE('예외 발생: 필수값이 누락 되었습니다.');
	WHEN NO_BOARD_ERROR THEN
		DBMS_OUTPUT.PUT_LINE('예외 발생: 존재하지 않는 게시판 입니다.');
	WHEN NO_SAVE_METHOD_ERROR THEN
		DBMS_OUTPUT.PUT_LINE('예외 발생: 존재하지 않는 저장 방법 입니다.');
	WHEN OTHERS THEN
		DBMS_OUTPUT.PUT_LINE('예외 발생: 기타 예외 : ' || SQLERRM);
END;
COMMIT;

